import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    def applicationName = "GOTZ"
    def time = new SimpleDateFormat('yyMMdd_HHmm').format(new Date())

    signingConfigs {
        debug {
            keyAlias 'key00'
            keyPassword 'gotzdebug'
            storeFile file("../gotz_debug.keystore")
            storePassword 'gotzdebug'
        }
        release {
            keyAlias 'key00'
            keyPassword 'gotzmaster'
            storeFile file("../gotz.keystore")
            storePassword 'gotzmaster'
        }
    }
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.gotz"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.gotzVersionCode
        versionName rootProject.gotzVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", "${applicationName}_${versionName}(${time})")
    }

    buildTypes {
        debug {
            debuggable = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            debuggable = false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures{
        dataBinding = true
        viewBinding = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${applicationName}_${versionName}(${time})-${variant.buildType.name}.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {

    implementation project(":base")
    implementation project(":data")
    implementation project(":domain")
    implementation project(":presentation")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // ViewModel, Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"
    implementation 'androidx.activity:activity-ktx:1.3.0'

    // Room
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    annotationProcessor "androidx.room:room-compiler:$rootProject.roomVersion"
    testImplementation "androidx.room:room-testing:$rootProject.roomVersion"

    // koin
    implementation "org.koin:koin-android:$rootProject.koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$rootProject.koinVersion"

    // dataStore
    implementation "androidx.datastore:datastore:$rootProject.dataStoreVersion"
    implementation "androidx.datastore:datastore-core:$rootProject.dataStoreVersion"
    implementation "androidx.datastore:datastore-rxjava2:$rootProject.dataStoreVersion"
    implementation "com.google.protobuf:protobuf-javalite:$rootProject.protocolBufferVersion"

}